import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    alias(libs.plugins.shadowjar)
    alias(libs.plugins.bukkit)
}

apply from: '../library.gradle'
apply from: '../publish.gradle'

dependencies {
    api projects.inventoryFrameworkPlatform
    runtimeOnly projects.inventoryFrameworkAnvilInput
    compileOnly libs.spigot
}

shadowJar {
    archiveBaseName.set('inventory-framework')
    archiveAppendix.set('bukkit')

    dependencies {
        exclude(dependency {
            it.moduleGroup == "org.jetbrains.kotlin"
        })
    }
}

bukkit {
    main = "me.devnatan.inventoryframework.runtime.InventoryFramework"
    name = "InventoryFramework"
    version = project.version
    description = "Minecraft Inventory API framework"
    website = "https://github.com/DevNatan/inventory-framework"
    apiVersion = "1.20"
    authors = ['SaiintBrisson', 'DevNatan', 'sasuked']
}