import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    alias(libs.plugins.shadowjar)
    alias(libs.plugins.kotlin)
}

apply from: '../library.gradle'
apply from: '../publish.gradle'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    api projects.inventoryFrameworkApi
    api projects.inventoryFrameworkCore
    api projects.inventoryFrameworkPlatform
    compileOnly libs.spigot
    testCompileOnly libs.spigot
    testRuntimeOnly libs.spigot
    testImplementation projects.inventoryFrameworkApi
    testImplementation projects.inventoryFrameworkTest
}

shadowJar {
    archiveBaseName.set('inventory-framework')
    archiveAppendix.set('bukkit')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter ReplaceTokens, tokens: [version: version]
    }
}

kotlin {
    explicitApi()
}