apply plugin: 'org.danilopianini.publish-on-central'

def isReleaseVersion = !version.endsWith("SNAPSHOT")

publishOnCentral {
    it.configureMavenCentral = true
    it.projectDescription = 'Inventory Framework is a Bukkit Inventory API framework that allows you to create custom inventories with varied interaction treatments.'
    it.projectLongName = project.name
    it.licenseName = 'MIT license'
    it.licenseUrl = 'https://github.com/DevNatan/inventory-framework/blob/main/LICENSE'
    it.repoOwner = 'DevNatan'
    it.projectUrl = 'https://github.com/' + it.repoOwner + '/' + project.name
    it.scmConnection = 'scm:git:https://github.com/' + it.repoOwner + '/' + project.name

    it.mavenCentral.user.set(System.getenv('OSSRH_USERNAME'))
    it.mavenCentral.password.set(provider { System.getenv('OSSRH_PASSWORD') })

    if (!isReleaseVersion) {
        it.mavenCentralSnapshotsRepository()
    }
}

/*
 * Developers and contributors must be added manually
 */
publishing {
    publications {
        javaOSSRH(MavenPublication) {
            it.groupId = rootProject.group
            it.artifactId = project.name.equals('framework')
                    ? rootProject.name + '-impl'
                    : (project.name.equals('bom') ? rootProject.name : rootProject.name + '-' + project.name)
            it.version = rootProject.version
//            it.from project.name.equals('bom') ? components["javaPlatform"] : components.java

            it.pom {
                developers {
                    developer {
                        name.set('Natan Vieira do Nascimento')
                        email.set('natanvnascimento@gmail.com')
                        url.set('https://github.com/DevNatan/')
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            project.findProperty('signing.keyId') ?: System.getenv('OSSRH_SIGNING_KEY'),
            project.findProperty('signing.password') ?: System.getenv('OSSRH_SIGNING_PASSWORD')
    )
}
