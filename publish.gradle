apply plugin: 'maven-publish'

project.afterEvaluate {
    final def project = it
    publishing {
        repositories {
            maven {
                name = 'GithubPackages'
                url = 'https://maven.pkg.github.com/DevNatan/inventory-framework'
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                configurePublication(project, it)
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

@SuppressWarnings('unused')
def configurePublication(Project project, Publication publication) {
    publication.groupId = 'com.github.DevNatan'

    final def base = 'inventory-framework'
    def fixedName = base + '-' + project.name
    if (project.name == 'bom') fixedName = base
    if (project.name == 'framework') fixedName = base + '-shared'

    publication.artifactId = fixedName

    publication.from components.java

    publication.pom {
        url.set('https://gihub.com/DevNatan/inventory-framework.git')
        licenses {
            license {
                name = 'MIT license'
                url = 'https://github.com/DevNatan/inventory-framework/blob/main/LICENSE'
            }
        }

        scm {
            connection = 'scm:git:git://github.com/DevNatan/inventory-framework.git'
            developerConnection = 'scm:git:ssh://github.com/DevNatan/inventory-framework.git'
            url = 'https://github.com/DevNatan/inventory-framework'
        }
    }
}